//Fluff structures serve no purpose and exist only for enriching the environment. They can be destroyed with a wrench.

/obj/structure/fluff
	name = "fluff structure"
	desc = ""
	icon_state = "minibar"
	anchored = TRUE
	density = FALSE
	opacity = 0
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 150
	var/deconstructible = TRUE

/obj/structure/fluff/pillow
	name = "pillows"
	desc = "Soft plush pillows. Resting your head on one is so relaxing."
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "pillow"
	density = FALSE

/obj/structure/fluff/pillow/red
	color = CLOTHING_RED

/obj/structure/fluff/pillow/blue
	color = CLOTHING_BLUE

/obj/structure/fluff/pillow/green
	color = CLOTHING_DARK_GREEN

/obj/structure/fluff/pillow/brown
	color = CLOTHING_BROWN

/obj/structure/fluff/pillow/magenta
	color = CLOTHING_MAGENTA

/obj/structure/fluff/pillow/purple
	color = CLOTHING_PURPLE

/obj/structure/fluff/pillow/black
	color = CLOTHING_BLACK

/obj/structure/fluff/drake_statue //Ash drake status spawn on either side of the necropolis gate in lavaland.
	name = "drake statue"
	desc = "Possibly the only time you'll ever see its likeness up close and live to tell the tale."
	icon = 'icons/effects/64x64.dmi'
	icon_state = "drake_statue"
	pixel_x = -16
	density = TRUE
	deconstructible = FALSE
	layer = EDGED_TURF_LAYER

/obj/structure/fluff/drake_statue/falling //A variety of statue in disrepair; parts are broken off and a gemstone is missing
	desc = ""
	icon_state = "drake_statue_falling"

/obj/structure/fluff/paper/corner
	icon_state = "papercorner"

/obj/structure/fluff/paper/stack
	name = "dense stack of papers"
	desc = "You can already feel your eyes glazing over and the boredom creeping in."
	icon_state = "paperstack"

/obj/structure/fluff/divine
	name = "Miracle"
	icon = 'icons/obj/hand_of_god_structures.dmi'
	anchored = TRUE
	density = TRUE

/obj/structure/fluff/divine/nexus
	name = "nexus"
	desc = ""
	icon_state = "nexus"

/obj/structure/fluff/divine/conduit
	name = "conduit"
	desc = ""
	icon_state = "conduit"

/obj/structure/fluff/divine/convertaltar
	name = "conversion altar"
	desc = ""
	icon_state = "convertaltar"
	density = FALSE
	can_buckle = 1

/obj/structure/fluff/divine/powerpylon
	name = "power pylon"
	desc = ""
	icon_state = "powerpylon"
	can_buckle = 1

/obj/structure/fluff/divine/defensepylon
	name = "defense pylon"
	desc = ""
	icon_state = "defensepylon"

/obj/structure/fluff/divine/shrine
	name = "shrine"
	desc = ""
	icon_state = "shrine"

/obj/structure/fluff/big_chain
	name = "giant chain"
	desc = ""
	icon = 'icons/effects/32x96.dmi'
	icon_state = "chain"
	layer = ABOVE_OBJ_LAYER
	anchored = TRUE
	density = TRUE
	deconstructible = FALSE

/obj/structure/fluff/railing
	name = "railing"
	desc = "A simple barrier of wood meant to prevent falls."
	icon = 'icons/obj/railing.dmi'
	icon_state = "railing"
	density = FALSE
	anchored = TRUE
	deconstructible = FALSE
	flags_1 = ON_BORDER_1
	climbable = TRUE
	layer = ABOVE_MOB_LAYER
	/// Living mobs can lay down to go past
	var/pass_crawl = TRUE
	/// Projectiles can go past
	var/pass_projectile = TRUE
	/// Throwing atoms can go past
	var/pass_throwing = TRUE
	/// Throwing/Flying non mobs can always exit the turf regardless of other flags
	var/allow_flying_outwards = TRUE
	/// Small flying mobs can fly over
	var/pass_flying = TRUE

/obj/structure/fluff/railing/do_climb(atom/movable/A)
	var/turf/climber_turf = get_turf(A)
	var/turf/dest
	if(climber_turf == src.loc)
		dest = get_step(src.loc, dir)
	else
		dest = src.loc
	if(!dest)
		return
	if(dest.is_blocked_turf(source_atom = A))
		if(ismob(A))
			to_chat(A, span_warning("Something is blocking the way."))
		return
	. = A.forceMove(dest)

/obj/structure/fluff/railing/Initialize(mapload)
	. = ..()
	init_connect_loc_element()
	var/lay = getwlayer(dir)
	if(lay)
		layer = lay

/obj/structure/fluff/railing/proc/init_connect_loc_element()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/railing/proc/getwlayer(dirin)
	switch(dirin)
		if(NORTH)
			layer = BELOW_MOB_LAYER-0.01
		if(WEST)
			layer = BELOW_MOB_LAYER
		if(EAST)
			layer = BELOW_MOB_LAYER
		if(SOUTH)
			layer = ABOVE_MOB_LAYER
			plane = GAME_PLANE_UPPER

/obj/structure/fluff/railing/CanPass(atom/movable/mover, turf/target)
//	if(istype(mover) && (mover.pass_flags & PASSTABLE))
//		return 1
	if(istype(mover, /obj/projectile))
		return 1
	if(mover.throwing)
		return 1
	if(isobserver(mover))
		return 1
	if(flown_over(mover))
		return 1
	if(isliving(mover))
		var/mob/living/M = mover
		if(!(M.mobility_flags & MOBILITY_STAND))
			if(pass_crawl)
				return TRUE
	if(icon_state == "woodrailing" && (dir in CORNERDIRS))
		var/list/baddirs = list()
		switch(dir)
			if(SOUTHEAST)
				baddirs = list(SOUTHEAST, SOUTH, EAST)
			if(SOUTHWEST)
				baddirs = list(SOUTHWEST, SOUTH, WEST)
			if(NORTHEAST)
				baddirs = list(NORTHEAST, NORTH, EAST)
			if(NORTHWEST)
				baddirs = list(NORTHWEST, NORTH, WEST)
		if(get_dir(loc, target) in baddirs)
			return 0
	else if(get_dir(loc, target) == dir)
		if(istype(mover, /mob/living/carbon/human/species/human/northern/goon))
			return TRUE // for the sake of Goons not getting stuck like Roombas
		else
			return FALSE
	return 1

/obj/structure/fluff/railing/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER

	if(dir in CORNERDIRS)
		return

	if(isobserver(leaving))
		return

	if(get_dir(leaving.loc, new_location) != dir)
		return

	if(pass_projectile && istype(leaving, /obj/projectile))
		return

	if(pass_throwing && leaving.throwing)
		return

	if(flown_over(leaving))
		return

	if(pass_crawl && isliving(leaving))
		var/mob/living/M = leaving
		if(!(M.mobility_flags & MOBILITY_STAND))
			return

	if(istype(leaving, /mob/living/carbon/human/species/human/northern/goon))
		return

	leaving.Bump(src)
	return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/railing/proc/flown_over(atom/movable/mover)
	if(!pass_flying || !isliving(mover))
		return FALSE
	var/mob/living/L = mover
	return (L.movement_type & FLYING) && L.mob_size <= MOB_SIZE_SMALL

/obj/structure/fluff/railing/OnCrafted(dirin)
	. = ..()
	var/lay = getwlayer(dir)
	if(lay)
		layer = lay

/obj/structure/fluff/railing/border/north
	dir = 1

/obj/structure/fluff/railing/border/east
	dir = 4

/obj/structure/fluff/railing/border/west
	dir = 8

/obj/structure/fluff/railing/corner
	icon_state = "border"
	density = FALSE
	dir = 9

/obj/structure/fluff/railing/corner/init_connect_loc_element()
	return

/obj/structure/fluff/railing/corner/north_east
	dir = 5

/obj/structure/fluff/railing/corner/south_west
	dir = 10

/obj/structure/fluff/railing/corner/south_east
	dir = 6

/obj/structure/fluff/railing/wood
	icon_state = "woodrailing"
	blade_dulling = DULLING_BASHCHOP
	layer = ABOVE_MOB_LAYER

/obj/structure/fluff/railing/wood/north
	dir = 1

/obj/structure/fluff/railing/wood/east
	dir = 4

/obj/structure/fluff/railing/wood/west
	dir = 8

/obj/structure/fluff/railing/stonehedge
	icon_state = "stonehedge"
	blade_dulling = DULLING_BASHCHOP
	layer = ABOVE_MOB_LAYER

/obj/structure/fluff/railing/border
	name = "border"
	desc = ""
	icon_state = "border"
	pass_crawl = FALSE

/obj/structure/fluff/railing/fence
	name = "palisade"
	desc = "A rudimentary barrier that might keep the monsters at bay."
	icon = 'icons/roguetown/misc/structure.dmi'
	attacked_sound = 'sound/misc/woodhit.ogg'
	destroy_sound = 'sound/misc/treefall.ogg'
	icon_state = "fence"
	density = TRUE
	opacity = TRUE
	anchored = TRUE
	layer = 2.91
	climbable = FALSE
	max_integrity = 400
	pass_crawl = FALSE
	pass_flying = FALSE
	climb_offset = 6

/obj/structure/fluff/railing/fence/Initialize(mapload)
	. = ..()
	smooth_fences()

/obj/structure/fluff/railing/fence/Destroy()
	..()
	smooth_fences()

/obj/structure/fluff/railing/fence/OnCrafted(dirin)
	. = ..()
	smooth_fences()

/obj/structure/fluff/railing/fence/proc/smooth_fences(neighbors)
	cut_overlays()
	if((dir == WEST) || (dir == EAST))
		var/turf/T = get_step(src, NORTH)
		if(T)
			for(var/obj/structure/fluff/railing/fence/F in T)
				if(F.dir == dir)
					if(!neighbors)
						F.smooth_fences(TRUE)
					var/mutable_appearance/MA = mutable_appearance(icon,"fence_smooth_above")
					MA.dir = dir
					add_overlay(MA)
		T = get_step(src, SOUTH)
		if(T)
			for(var/obj/structure/fluff/railing/fence/F in T)
				if(F.dir == dir)
					if(!neighbors)
						F.smooth_fences(TRUE)
					var/mutable_appearance/MA = mutable_appearance(icon,"fence_smooth_below")
					MA.dir = dir
					add_overlay(MA)

/obj/structure/fluff/railing/fence/CanPass(atom/movable/mover, turf/target)
	if(get_dir(loc, target) == dir)
		return 0
	return 1

/obj/structure/bars
	name = "bars"
	desc = "Rigid metal bars, intended to impair access to somewhere."
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "bars"
	density = TRUE
	anchored = TRUE
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 1400
	damage_deflection = 12
	integrity_failure = 0.15
	dir = SOUTH
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	obj_flags = CAN_BE_HIT | BLOCK_Z_OUT_DOWN
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")

/obj/structure/bars/obj_break(damage_flag)
	loud_message("A sickening, metallic scrape of bars getting broken rings out", hearing_distance = 14)
	. = ..()

/obj/structure/bars/CanPass(atom/movable/mover, turf/target)
	if(isobserver(mover))
		return 1
	if(istype(mover) && (mover.pass_flags & PASSGRILLE))
		return 1
	if(mover.throwing && !ismob(mover))
		return prob(66)
	return ..()

/obj/structure/bars/shop
	icon_state = "barsbent"
	layer = BELOW_OBJ_LAYER

/obj/structure/bars/shoptwo
	icon_state = "barsshop"
	layer = BELOW_OBJ_LAYER

/obj/structure/bars/shoptwo/obj_break(damage_flag)
	icon_state = "barsshopb"
	density = FALSE
	..()

/obj/structure/bars/shop/bronze
	color = "#ff9c1a"

/obj/structure/bars/chainlink
	icon_state = "chainlink"

/obj/structure/bars/steel
	name = "steel bars"
	max_integrity = 2500

/obj/structure/bars/tough
	max_integrity = 9000
	damage_deflection = 40

/*
/obj/structure/bars/CheckExit(atom/movable/O, turf/target)
	if(istype(O) && (O.pass_flags & PASSGRILLE))
		return 1
	if(O.throwing && !ismob(O))
		return 1
	return !density
	..()
*/
/obj/structure/bars/obj_break(damage_flag)
	if(isnull(broken_icon_state))
		icon_state = "[initial(icon_state)]b"
	else
		icon_state = broken_icon_state
	density = FALSE
	..()

/obj/structure/bars/cemetery
	icon_state = "cemetery"

/obj/structure/bars/passage
	icon_state = "passage0"
	desc = "This looks like it can open and close!"
	density = TRUE
	max_integrity = 2000
	redstone_structure = TRUE
	broken_icon_state = "passage1b"

/obj/structure/bars/passage/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Most gates are traditionally linked to a lever or winch. Left-clicking the right lever or winch will open the gate that they're connected to.")
	. += span_info("A skilled Engineer could use a wrench to link this to a device.")
	. += span_info("The Master of the Guild of Craft can unlink devices from each other by using their special wrench.")
	. += span_info("While a length process, gates can also be bypassed through destroying them with enough strikes. Bombs of blastpowder in particular excel at damaging these structures.")

/obj/structure/bars/passage/steel
	name = "steel bars"
	max_integrity = 2500

/obj/structure/bars/passage/redstone_triggered()
	if(obj_broken)
		return
	if(density)
		icon_state = "passage1"
		density = FALSE
	else
		icon_state = "passage0"
		density = TRUE

/obj/structure/bars/passage/shutter
	icon_state = "shutter0"
	density = TRUE
	opacity = TRUE
	redstone_structure = TRUE

/obj/structure/bars/passage/shutter/redstone_triggered()
	if(obj_broken)
		return
	if(density)
		icon_state = "shutter1"
		density = FALSE
		set_opacity(FALSE)
	else
		icon_state = "shutter0"
		density = TRUE
		set_opacity(TRUE)

/obj/structure/bars/passage/shutter/open
	icon_state = "shutter1"
	density = FALSE
	opacity = FALSE

/obj/structure/bars/passage/attackby(obj/item/I, mob/user, params)
	. = ..()
	var/obj/item = user.get_active_held_item()
	if(user.used_intent.type == /datum/intent/chisel )
		if (user.get_skill_level(/datum/skill/craft/engineering) <= 3)
			to_chat(user, span_warning("I need more skill to carve a name into this passage."))
			return
		playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		user.visible_message("<span class='info'>[user] Carves a name into the passage.</span>")
		if(do_after(user, 10))
			var/passagename
			passagename = sanitize(input(user, "What name would you like to carve into the passage?"))
			if (passagename)
				name = passagename + "(passage)"
				desc = "a passage with a name carved into it"
			else
				name = "passage"
				desc = "a passage with a carving scratched out"
			playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		return
	else if(istype(item, /obj/item/rogueweapon/chisel/assembly))
		to_chat(user, span_warning("You most use both hands to rename the passage."))

/obj/structure/bars/grille
	name = "grille"
	desc = ""
	icon_state = "floorgrille"
	density = FALSE
	//layer = TABLE_LAYER
	plane = GAME_PLANE
	damage_deflection = 5
	blade_dulling = DULLING_BASHCHOP
	obj_flags = CAN_BE_HIT | BLOCK_Z_OUT_DOWN | BLOCK_Z_IN_UP
	attacked_sound = list('sound/combat/hits/onmetal/grille (1).ogg', 'sound/combat/hits/onmetal/grille (2).ogg', 'sound/combat/hits/onmetal/grille (3).ogg')
	var/togg = FALSE
	redstone_structure = TRUE

/obj/structure/bars/grille/Initialize(mapload)
	AddComponent(/datum/component/squeak, list('sound/foley/footsteps/FTMET_A1.ogg','sound/foley/footsteps/FTMET_A2.ogg','sound/foley/footsteps/FTMET_A3.ogg','sound/foley/footsteps/FTMET_A4.ogg'), 40)
	dir = pick(GLOB.cardinals)
	return ..()

/obj/structure/bars/grille/obj_break(damage_flag)
	obj_flags = CAN_BE_HIT
	..()

/obj/structure/bars/grille/redstone_triggered()
	if(obj_broken)
		return

	togg = !togg
	playsound(src, 'sound/foley/trap_arm.ogg', 100)
	if(togg)

		icon_state = "floorgrilleopen"
		set_is_platform(FALSE)
		obj_flags &= ~BLOCK_Z_IN_UP
		var/turf/T = loc
		if(istype(T))
			for(var/mob/living/M in loc)
				T.Entered(M)
	else

		icon_state = "floorgrille"
		set_is_platform(TRUE)
		obj_flags |= BLOCK_Z_IN_UP

/obj/structure/bars/grille/attackby(obj/item/I, mob/user, params)
	. = ..()
	var/obj/item = user.get_active_held_item()
	if(user.used_intent.type == /datum/intent/chisel )
		if (user.get_skill_level(/datum/skill/craft/engineering) <= 3)
			to_chat(user, span_warning("I need more skill to carve a name into this grille."))
			return
		playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		user.visible_message("<span class='info'>[user] Carves a name into the grille.</span>")
		if(do_after(user, 10))
			var/grillename
			grillename = sanitize(input(user, "What name would you like to carve into the grille?"))
			if (grillename)
				name = grillename + "(grille)"
				desc = "a grille with a name carved into it"
			else
				name = "grille"
				desc = "a grille with a carving scratched out"
			playsound(user, 'sound/misc/wood_saw.ogg', 100, TRUE)
		return
	else if(istype(item, /obj/item/rogueweapon/chisel/assembly))
		to_chat(user, span_warning("You most use both hands to rename the grille."))

/obj/structure/bars/pipe
	name = "bronze pipe"
	desc = "Bronze pipework. Plumbing was once a more ubiquitous technology than it is now."
	icon_state = "pipe"
	density = FALSE
	layer = TABLE_LAYER
	plane = GAME_PLANE
	damage_deflection = 5
	blade_dulling = DULLING_BASHCHOP
	obj_flags = CAN_BE_HIT | BLOCK_Z_OUT_DOWN | BLOCK_Z_IN_UP
	attacked_sound = list('sound/combat/hits/onmetal/grille (1).ogg', 'sound/combat/hits/onmetal/grille (2).ogg', 'sound/combat/hits/onmetal/grille (3).ogg')
	var/togg = FALSE

/obj/structure/bars/pipe/left
	name = "bronze pipe"
	icon_state = "pipe2"
	dir = WEST
	pixel_x = 19

//===========================

/obj/structure/fluff/clock
	name = "clock"
	desc = "A large grandfather clock; the cutting edge of modern technology."
	icon = 'icons/roguetown/misc/tallstructure.dmi'
	icon_state = "clock"
	density = FALSE
	anchored = FALSE
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 100
	integrity_failure = 0.5
	dir = SOUTH
	break_sound = "glassbreak"
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = 'sound/combat/hits/onglass/glasshit.ogg'
	var/datum/looping_sound/clockloop/soundloop
	drag_slowdown = 3

/obj/structure/fluff/clock/Initialize(mapload)
	soundloop = new(src, FALSE)
	soundloop.start()
	. = ..()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/clock/Destroy()
	if(soundloop)
		soundloop.stop()
	..()

/obj/structure/fluff/clock/obj_break(damage_flag)
	icon_state = "b[initial(icon_state)]"
	if(soundloop)
		soundloop.stop()
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')
	..()

/obj/structure/fluff/clock/attack_right(mob/user)
	handle_special_items_retrieval(user, src)
	return

/obj/structure/fluff/clock/examine(mob/user)
	. = ..()
	if(obj_broken)
		return
	var/day = LOWER_TEXT(get_current_day_of_week_name())
	. += "Oh no, it's [station_time_timestamp("hh:mm")] on a [day]"
//		if(SSshuttle.emergency.mode == SHUTTLE_DOCKED)
//			if(SSshuttle.emergency.timeLeft() < 30 MINUTES)
//				. += span_warning("The last boat will leave in [round(SSshuttle.emergency.timeLeft()/600)] minutes.")

/obj/structure/fluff/clock/CanAStarPass(ID, to_dir, caller)
	if(to_dir == dir)
		return FALSE // don't even bother climbing over it
	return ..()

/obj/structure/fluff/clock/CanPass(atom/movable/mover, turf/target)
	if(get_dir(loc, mover) == dir)
		return 0
	return 1

/obj/structure/fluff/clock/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER
	if(get_dir(leaving.loc, new_location) == dir)
		leaving.Bump(src)
		return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/wallclock
	name = "clock"
	desc = "Second greatest of all tyrants."
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "wallclock"
	density = FALSE
	anchored = TRUE
	layer = BELOW_MOB_LAYER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 100
	integrity_failure = 0.5
	var/datum/looping_sound/clockloop/soundloop
	break_sound = "glassbreak"
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = 'sound/combat/hits/onglass/glasshit.ogg'
	pixel_y = 32

/obj/structure/fluff/wallclock/attack_right(mob/user)
	handle_special_items_retrieval(user, src)
	return

/obj/structure/fluff/wallclock/Destroy()
	if(soundloop)
		soundloop.stop()
	..()

/obj/structure/fluff/wallclock/examine(mob/user)
	. = ..()
	if(obj_broken)
		return
	var/day = LOWER_TEXT(get_current_day_of_week_name())
	. += "Oh no, it's [station_time_timestamp("hh:mm")] on a [day]"

/obj/structure/fluff/wallclock/Initialize(mapload)
	soundloop = new(src, FALSE)
	soundloop.start()
	. = ..()

/obj/structure/fluff/wallclock/obj_break(damage_flag)
	icon_state = "b[initial(icon_state)]"
	if(soundloop)
		soundloop.stop()
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')
	..()

/obj/structure/fluff/wallclock/l
	pixel_y = 0
	pixel_x = -32
/obj/structure/fluff/wallclock/r
	pixel_y = 0
	pixel_x = 32
//vampire
/obj/structure/fluff/wallclock/vampire
	name = "ancient clock"
	desc = ""
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "wallclockvampire"
	density = FALSE
	anchored = TRUE
	layer = BELOW_MOB_LAYER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 100
	integrity_failure = 0.5
	pixel_y = 32

/obj/structure/fluff/wallclock/vampire/l
	pixel_y = 0
	pixel_x = -32
/obj/structure/fluff/wallclock/vampire/r
	pixel_y = 0
	pixel_x = 32

/obj/structure/fluff/signage
	name = "sign"
	desc = "It's a sign! It seems to be pointing somewhere."
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "shitsign"
	density = TRUE
	anchored = TRUE
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 500
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')

/obj/structure/fluff/sign
	icon_state = "signwrote"
	name = "sign"
	desc = "It's a sign! These usually have words carved into them."
	icon = 'icons/roguetown/misc/structure.dmi'

/obj/structure/fluff/customsign
	name = "sign"
	desc = "It's a sign! It looks like it'd be quite easy to carve your \
	own message into this one, were you so inclined."
	icon_state = "sign"
	var/wrotesign
	max_integrity = 500
	blade_dulling = DULLING_BASHCHOP
	icon = 'icons/roguetown/misc/structure.dmi'

/obj/structure/fluff/customsign/examine(mob/user)
	. = ..()
	if(wrotesign)
		if(!user.is_literate())
			. += "I have no idea what it says."
		else
			. += "It says \"[wrotesign]\"."

/obj/structure/fluff/customsign/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Left clicking on the sign with a dagger on STAB intent allows you to carve a message into it!")

/obj/structure/fluff/customsign/attackby(obj/item/W, mob/user, params)
	if(!user.cmode)
		if(!user.is_literate())
			to_chat(user, span_warning("I do not know how to write."))
			return
		if((user.used_intent.blade_class == BCLASS_STAB) && (W.wlength == WLENGTH_SHORT))
			if(wrotesign)
				to_chat(user, span_warning("Something is already carved here."))
				return
			else
				var/inputty = stripped_input(user, "What would you like to carve here?", "", null, 200)
				if(inputty && !wrotesign)
					wrotesign = inputty
					icon_state = "signwrote"
		else
			to_chat(user, span_warning("Alas, this will not work. I could carve words, if I stabbed at this with something posessing a short, sharp point. A knife comes to mind."))
			return
	..()

/obj/structure/fluff/alch
	name = "alchemical lab"
	desc = "A stout workstation arrayed with alchemical parahenalia and equipment. Some say the truest heights of the \
	Art were reached in times immemorial, and shall never be again."
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "alch"
	density = TRUE
	anchored = TRUE
	layer = BELOW_OBJ_LAYER
	blade_dulling = DULLING_BASHCHOP
	max_integrity = 450
	destroy_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")

/obj/structure/fluff/alch/folding
	name = "folding alchemical lab"
	desc = "A compact laboratory. Laid out and ready to work."
	icon = 'icons/roguetown/misc/gadgets.dmi'
	icon_state = "foldingAlchstationDeployed"
	max_integrity = 350
	debris = list(/obj/item/grown/log/tree/small = 2)
	climbable = TRUE
	climb_offset = 10

/obj/structure/fluff/alch/folding/examine()
	. = ..()
	. += span_blue("Right-Click to fold the lab.")

/obj/structure/fluff/alch/folding/attack_right(mob/user)
	if(do_after(user, 5 SECONDS, target = src))
		user.visible_message(span_notice("[user] folds [src]."), span_notice("You fold [src]."))
		new /obj/item/folding_alchstation_stored(drop_location())
		qdel(src)
		return ..()

/obj/structure/fluff/alch/trans
	name = "transmutation lab"
	icon = 'icons/roguetown/misc/alchemy.dmi' // temporary deconflict
	icon_state = "transgender" // i love the artist that did this
	desc = "Metal and glass, porcelain and gilbranze, copper and bronze - a chorus for works, Great and Lesser. \
	The greatest of them have been lost to time.\n\nA secure pair of drums hold your catalytic samples in metal \
	tubes, held still from all things by their inner arcynic constructs. The cabinet beneath is simply mundane, though no less useful."
	var/cur_user		 						// avoiding tgui headaches right off the bat. update THIS DID NOT SAVE THIS ONE
	var/datum/tgui/current_ui					// to avoid edge cases. will it work? idfk
	// data for crafting menu begins here
	var/cached_craftability
	var/last_surroundings_hash
	var/selected_catalyst						// i hate frontend code what the fuck do you mean this has to be stored on the backend aaaa
	// data for puzzle menu begins here
	var/obj/item/seed_item						// item we're trying to turn into a catalyst
	var/obj/item/alch/catalyst/current_recipe	// this will be a path! not an actual item!
	var/list/selected_steps						// current selections for the recipe
	var/list/last_attempt						// your last guess, stored so we can display it below the input
	var/list/step_titles = list("harmonize", "sanguinate", "raefy", "platonize", "distill")

/obj/structure/fluff/alch/trans/ComponentInitialize()
	. = ..()
	AddComponent(/datum/component/storage/concrete/roguetown/trans) // to store our transmutation catalysts in. what did you think it meant?

/obj/structure/fluff/alch/trans/uni/Initialize(mapload) // bit less roundstart gruntwork for uni, not enough to make metals n such
	. = ..()
	var/datum/component/storage/catalyst_storage = GetComponent(/datum/component/storage)
	catalyst_storage.handle_item_insertion(new /obj/item/alch/catalyst/florid(loc))
	catalyst_storage.handle_item_insertion(new /obj/item/alch/catalyst/terran(loc))
	catalyst_storage.handle_item_insertion(new /obj/item/alch/catalyst/nigredo(loc))

/obj/structure/fluff/alch/trans/get_mechanics_examine(mob/user)
	. = ..()
	if(!can_transmute(user)) // this is real alchemical shit get out of here novices
		return
	. += span_info("Standing nearby allows one to craft basic alchemical recipes.")
	. += span_info("Left click to open the interface. Within, you can create catalysts and perform transmutation. See the Encyclopedia for more details.")
	. += span_info("Drag the workstation to yourself to open the catalyst inventory. Catalysts stored here will enable transmutation recipes for the workstation.")
	. += span_info("Right click to cancel a catalyzation experiment, refunding the seed item. Close the catalyzation window first!")

/obj/structure/fluff/alch/trans/attackby(obj/item/with, mob/user, params)
	. = ..()
	if(istype(with, /obj/item/alch/catalyst))
		var/datum/component/storage/catalyst_storage = GetComponent(/datum/component/storage)
		if(catalyst_storage)
			if(catalyst_storage.handle_item_insertion(with, FALSE, user))
				return
	if(!can_transmute(user)) // obligatory
		to_chat(user, span_warning("I have no idea how to use this."))
		return
	if(user.ensure_skills().get_skill_level(/datum/skill/craft/alchemy) < SKILL_LEVEL_EXPERT)
		to_chat(user, span_warning("I'm not quite skilled enough to create my own catalysts. It takes an alchemical expert to do so safely."))
		return
	if(current_recipe)
		if(!istype(with, /obj/item/alch/catalyzation_reagent))
			return
		// attempt to create a catalyst
		for(var/idx in selected_steps)
			if(idx == 0)
				to_chat(user, span_warning("I don't think this is the right number of steps for this...")) // we are nice here and don't eat your dust bcs this is probably a misclick
				return
		var/matches = TRUE
		for(var/idx in 1 to length(selected_steps))
			last_attempt[idx] = selected_steps[idx]
			if(selected_steps[idx]!=GLOB.catalyst_recipes[current_recipe][idx])
				matches = FALSE
		if(matches)
			var/obj/item/alch/catalyst/result = new current_recipe(loc)
			var/steps_desc = step_titles[selected_steps[1]]
			for(var/idx in 2 to length(selected_steps))
				steps_desc += (idx == length(selected_steps) ? ", and ": ", ") // doing this inside the loop means we handle the extreme edge case of a 2-step recipe
				steps_desc += step_titles[selected_steps[idx]]
			user.visible_message(
				span_notice("[user] transforms \the [seed_item] into \a [result]!"),
				span_notice("\The [with] glows softly as I apply it to the catalyzation equipment. I carefully [steps_desc] \the [seed_item], transforming it into \a [result]!")
				)
			current_ui?.close() // end of the flow, everyone get out so we can be prepped for next time
			QDEL_NULL(seed_item)
			current_recipe = null
			selected_steps = null
			last_attempt = null
			QDEL_NULL(with)
			return
		to_chat(user, span_warning("You sprinkle [with] over the catalyzation equipment, causing some of it to glow. Seems this setup won't do what you want..."))
		QDEL_NULL(with)
		return
	if(seed_item)
		to_chat(user, span_warning("[src] already has an item placed on it for work!"))
		return
	if(cur_user) // should be impossible with these checks
		to_chat(user, span_warning("Someone is already using this."))
		return
	if(HAS_TRAIT(with, TRAIT_NODROP)) // lol. lmao.
		to_chat(user, span_warning("I can't get [with] onto the table for processing!"))
		return
	for(var/obj/item/alch/catalyst/path as anything in typesof(/obj/item/alch/catalyst))
		if(path::seed_item && ispath(with.type, path::seed_item))
			current_recipe=path
			seed_item = with
			seed_item.forceMove(src)
			user.visible_message(
				span_notice("[user] places [seed_item] on [src], preparing to work!"),
				span_notice("I place [seed_item] on [src] and clamp [src.gender==PLURAL ? "them" : "it"] in place, preparing to work.")
				)
			selected_steps = list()
			last_attempt = list()
			for(var/idx in 1 to current_recipe::difficulty)
				selected_steps += 0
				last_attempt += 0
			break
	if(!current_recipe)
		to_chat(user, span_warning("This doesn't seem like it'd make a useful catalyst..."))
		return
	ui_interact(user)

/obj/structure/fluff/alch/trans/attack_right(mob/user, list/modifiers)
	if(seed_item)
		if(current_ui)
			current_ui.close()
		user.put_in_hands(seed_item)
		to_chat(user, span_notice("I carefully detach [seed_item] from [src]."))
		seed_item = null
		current_recipe = null
		cur_user = null
		current_ui = null
		return TRUE
	return ..()

/obj/structure/fluff/alch/trans/attack_hand(mob/user)
	. = ..()
	if(.)
		return
	if(cur_user)
		to_chat(user, span_warning("Someone is already using this."))
		return
	if(!can_transmute(user))
		to_chat(user, span_warning("I have no idea how to use this."))
		return
	if(ui_interact(user))
		cur_user = user
	return

/obj/structure/fluff/alch/trans/ui_close(mob/user)
	. = ..()
	cur_user = null
	current_ui = null

/obj/structure/fluff/alch/trans/ui_interact(mob/user, datum/tgui/ui)
	var/menu_type = (current_recipe ? "Catalyzation" : "TransCraft")
	var/window_title = (current_recipe ? "Catalyzation" : "Transmutation Menu")

	ui = SStgui.try_update_ui(user, src, ui)
	if(ui && ui.interface!=menu_type)
		ui.close()
		ui = null
	if(!ui)
		ui = new(user, src, menu_type, window_title)
		ui.set_state(GLOB.not_incapacitated_turf_state)
		ui.open()
	current_ui = ui
	return current_ui

/obj/structure/fluff/alch/trans/ui_act(action, params)
	. = ..()
	switch(action)
		if("setcatalyst")
			selected_catalyst = params["catalyst"]
			return
		if("craft")
			var/path = text2path(params["item"])
			var/amount = params["amount"] || 1
			var/auto = params["auto"]
			transmute(usr, path, amount, auto)
		if("add_step")
			var/step = params["id"]
			if(step)
				var/idx=selected_steps.Find(0)
				if(idx)
					selected_steps[idx] = step
		if("del_step")
			var/idx = params["id"]
			if(idx && idx <= length(selected_steps))
				selected_steps[idx] = 0

/obj/structure/fluff/alch/trans/proc/transmute(mob/living/carbon/human/user, datum/transmutation_recipe/path, amount = 1, auto)
	if(!user || !istype(user))
		return
	if(get_dist(user, src) > 1)
		to_chat(user, span_warning("Too far away!"))
		return
	var/datum/transmutation_recipe/recipe
	for(var/datum/transmutation_recipe/A in GLOB.transmutation_recipes)
		if(istype(A, path))
			recipe = A
			break
	if(!recipe)
		return
	while(amount > 0 || auto)
		amount--
		var/result = transmute_attempt(user, recipe)
		if(!result)
			return

/obj/structure/fluff/alch/trans/proc/transmute_attempt(mob/living/carbon/human/user, datum/transmutation_recipe/R)
	var/datum/component/storage/catalyst_storage = GetComponent(/datum/component/storage)
	var/found = FALSE
	for(var/obj/item/alch/catalyst/catalyst in catalyst_storage.contents())
		if(ispath(catalyst.type, R.catalyst))
			found = TRUE
	if(!found)
		to_chat(user, span_warning("Lacking \a [R.catalyst::name] to craft!"))
		return FALSE
	if (HAS_TRAIT(user, TRAIT_CURSE_MALUM))
		to_chat(user, span_warning("Your cursed hands tremble and fail to craft... Malum forbids it."))
		return
	if(user.doing)
		return
	if(!can_transmute(user) || (user.get_skill_level(/datum/skill/craft/alchemy) < R.skill_required))
		to_chat(user, span_warning("I'm not experienced enough with transmutation to craft this!"))
		return FALSE
	var/list/ingredients = gather_ingredients(user, R)
	if(!ingredients)
		to_chat(user, span_warning("Not enough items to craft!"))
		return FALSE
	var/list/materia_ingredients = gather_materia(user, R, ingredients)
	if(!materia_ingredients)
		to_chat(user, span_warning("Missing <i>prima materia</i> to craft!"))
		return FALSE
	playsound(loc, 'sound/foley/scribble.ogg', 100, TRUE)
	if(!do_after(user, 1 SECONDS, target = user))
		to_chat(user, span_warning("Must stay still to craft!"))
		return FALSE
	ingredients = gather_ingredients(user, R)
	if(!ingredients)
		to_chat(user, span_warning("Not enough items to craft!"))
		return FALSE
	materia_ingredients = gather_materia(user, R, ingredients)
	if(!materia_ingredients)
		to_chat(user, span_warning("Missing <i>prima materia</i> to craft!"))
		return FALSE
	R.create_outputs(user, ingredients, materia_ingredients, src)
	if(user.mind && isliving(user))
		var/mob/living/L = user
		var/amt2raise = (L.STAINT * 2) + (R.skill_required * 10)
		if(amt2raise > 0)
			user.mind.add_sleep_experience(/datum/skill/craft/alchemy, amt2raise, FALSE)
	return TRUE

/// returns the array index of the path I is a child path of, or null if it's not in the list
/obj/structure/fluff/alch/trans/proc/find_in_reqs(obj/item/I, list/reqs)
	for(var/path in reqs)
		if(ispath(I.type, path))
			return path
		if(ispath(path, /obj/item/rogueore/gold) && istype(I, /obj/item/goldslag)) // special case, this is an alt gold ore item
			return path

/obj/structure/fluff/alch/trans/proc/gather_ingredients(mob/living/carbon/human/user, datum/transmutation_recipe/R) // sure am glad we cache this
	var/list/ingredients = list()
	var/list/needed_items = R.input_items.Copy()
	var/list/env_items = get_environment(user)
	var/list/single_items = list()
	var/list/bundles = list() // don't... don't ask. it hurts.
	for(var/obj/item/I in env_items)
		if(R.validate_ingredient(I))
			return list(I) // this is a snowflake recipe
		if(I.can_craft_with() && (find_in_reqs(I, needed_items) || R.validate_ingredient(I)))
			single_items += I
		else if(istype(I, /obj/item/natural/bundle) || R.validate_ingredient(I))
			var/obj/item/natural/bundle/B = I
			if(needed_items[B.stacktype])
				bundles += I
		else if(istype(I, /obj/item/construction/bundle) || R.validate_ingredient(I))
			var/obj/item/construction/bundle/B = I
			if(needed_items[B.stacktype])
				bundles += I
	for(var/obj/item/bundle in bundles) // we prioritize bundles first...
		var/obj/item/natural/bundle/bundlethesecond = bundle
		var/obj/item/construction/bundle/bundlethethird = bundle
		if(istype(bundlethesecond))
			needed_items[bundlethesecond.stacktype] -= min(bundlethesecond.amount, needed_items[bundlethesecond.stacktype]) // transmuting with bundles is lossy
			ingredients += bundlethesecond
		if(istype(bundlethethird))
			needed_items[bundlethethird.stacktype] -= min(bundlethethird.amount, needed_items[bundlethethird.stacktype]) // transmuting with bundles is lossy
			ingredients += bundlethethird
	var/list/materiaful_items = list()
	for(var/obj/item/I in single_items) // ...then single items that don't contain needed materia, to avoid edge cases where an item could work for ingredient and materia, causing the first one to pick it to win...
		var/cont = FALSE
		var/used_as = find_in_reqs(I, needed_items)
		if(needed_items[used_as]<=0)
			continue
		for(var/aspect as anything in I.materia)
			if(R.materia_aspects.Find(aspect))
				materiaful_items += I
				cont = TRUE
		if(cont)
			continue
		ingredients += I
		needed_items[used_as] -= 1
	for(var/obj/item/I in materiaful_items) // ...then the rest
		var/used_as = find_in_reqs(I, needed_items)
		if(needed_items[used_as]<=0)
			continue
		ingredients += I
		needed_items[used_as] -= 1
	// after we've checked all the valid items, if we're short, no dice
	for(var/path in needed_items)
		if(needed_items[path])
			return FALSE
	return ingredients

/obj/structure/fluff/alch/trans/proc/gather_materia(mob/living/carbon/human/user, datum/transmutation_recipe/R, list/ingredients)
	var/list/env_items = get_environment(user)
	var/list/materia_reqs = R.materia_aspects.Copy()
	var/list/materia_items = list()
	env_items.RemoveAll(ingredients)
	if(!length(env_items))
		return FALSE
	for(var/obj/item/I in env_items)
		if(!length(I.materia))
			continue
		if((I.type in R.output_items) && !R.allow_output_materia)
			continue
		for(var/path in I.materia)
			if(materia_reqs.Find(path))
				materia_items |= list(I)
				materia_reqs.Remove(path)
	if(length(materia_reqs))
		return FALSE
	return materia_items

/obj/structure/fluff/alch/trans/ui_data(mob/user)
	if(!current_ui)
		return
	if(current_ui.interface == "TransCraft")
		return transcraft_data(user)
	else
		return catalyzation_data(user)

/obj/structure/fluff/alch/trans/proc/catalyzation_data(user)
	var/list/data = list()
	data["recipe"] = (current_recipe ? current_recipe::name : null)
	data["difficulty"] = (current_recipe ? current_recipe::difficulty : null)
	data["current_steps"] = selected_steps
	data["history"] = last_attempt
	data["answer"] = (current_recipe ? GLOB.catalyst_recipes[current_recipe] : null)
	return data

/obj/structure/fluff/alch/trans/proc/transcraft_data(user)
	var/list/data = list()
	data["selectedcatalyst"] = selected_catalyst

	var/list/surroundings = get_environment(user)
	var/new_hash = list2params(surroundings)

	var/list/catalysts = list()
	var/datum/component/storage/catalyst_storage = GetComponent(/datum/component/storage)
	for(var/obj/item/alch/catalyst/catalyst in catalyst_storage.contents())
		catalysts[catalyst.name] = catalyst.icon_state
	data["catalysts"] = catalysts

	if(new_hash == last_surroundings_hash && cached_craftability)
		data["craftability"] = cached_craftability
		return data

	last_surroundings_hash = new_hash
	var/list/craftability = list()
	for(var/rec in GLOB.transmutation_recipes)
		var/datum/transmutation_recipe/R = rec
		var/list/ings = gather_ingredients(user, R)
		var/list/mats = gather_materia(user, R, ings)
		craftability[R::name] = (islist(ings) && islist(mats))

	cached_craftability = craftability
	data["craftability"] = craftability
	return data

/obj/structure/fluff/alch/trans/proc/get_surroundings(mob/user)
	. = list()
	.["tool_behaviour"] = list()
	.["other"] = list()
	.["materia"] = list()
	for(var/obj/item/I in get_environment(user))
		if(!I.can_craft_with())
			continue
		if(I.flags_1 & HOLOGRAM_1)
			continue
		if(length(I.materia))
			for(var/aspect in I.materia)
				if(!.["materia"][aspect])
					.["materia"][aspect] = list(I)
				else
					.["materia"][aspect] += I
		if(istype(I, /obj/item/natural/bundle))
			var/obj/item/natural/bundle/B = I
			.["other"][B.stacktype] += B.amount
		else if(istype(I, /obj/item/construction/bundle))
			var/obj/item/construction/bundle/B = I
			.["other"][B.stacktype] += B.amount
		else if(I.tool_behaviour)
			.["tool_behaviour"] += I.tool_behaviour
			.["other"][I.type] += 1
		else
			if(istype(I, /obj/item/reagent_containers))
				var/obj/item/reagent_containers/RC = I
				if(RC.is_drainable())
					for(var/datum/reagent/A in RC.reagents.reagent_list)
						.["other"][A.type] += A.volume
				if(istype(RC, /obj/item/reagent_containers/glass)) // Only count glass bottles themselves as a valid crafting item if it's empty
					if(RC.reagents.total_volume == 0)
						.["other"][I.type] += 1
				else
					.["other"][I.type] += 1
			else
				.["other"][I.type] += 1

/obj/structure/fluff/alch/trans/proc/get_environment(mob/user)
	. = list()
	for(var/obj/item/I in user.held_items)
		. += I
	if(!isturf(user.loc))
		return
	var/list/L = block(get_step(user, SOUTHWEST), get_step(user, NORTHEAST))
	for(var/A in L)
		var/turf/T = A
		if(T.Adjacent(user))
			for(var/B in T)
				var/atom/movable/AM = B
				if(AM.flags_1 & HOLOGRAM_1)
					continue
				. += AM
				var/list/crafting_items = AM.get_crafting_contents()
				if(crafting_items)
					for(var/atom/movable/crafting_item as anything in crafting_items)
						. += crafting_item
	for(var/slot in list(SLOT_R_STORE, SLOT_L_STORE))
		. += user.get_item_by_slot(slot)
	for(var/obj/item/I in .)
		if(!I.can_craft_with())
			. -= I

/obj/structure/fluff/alch/trans/ui_static_data(mob/user)
	var/list/data = list()

	var/list/transmutation_recipes = list()
	for(var/datum/transmutation_recipe/R as anything in GLOB.transmutation_recipes)
		if(!R.name)
			continue
		if(is_abstract(R))
			continue
		if(R.snowflake_hidden)
			continue
		transmutation_recipes[R.type] = R.cached_display_data

	data["transmutation_recipes"] = transmutation_recipes
	return data

// we don't check materia here, so recipes will show up as craftable if their item inputs are present but no materia is. this is fine because i bca to implement bespoke materia checks here
/obj/structure/fluff/alch/trans/proc/check_contents(datum/transmutation_recipe/R, list/contents)
	contents = contents["other"]
	main_loop:
		for(var/A in R.input_items)
			var/needed_amount = R.input_items[A]
			for(var/B in contents)
				if(ispath(B, A))
					if(!R.subtype_reqs && (B in subtypesof(A)))
						continue
					if(contents[B] >= R.input_items[A])
						continue main_loop
					else
						needed_amount -= contents[B]
						if(needed_amount <= 0)
							continue main_loop
						else
							continue
			return FALSE
	return TRUE

/obj/structure/fluff/statue
	name = "statue"
	desc = "Dead stone designed to compel living minds."
	icon = 'icons/roguetown/misc/tallstructure.dmi'
	icon_state = "bstatue"
	density = FALSE
	anchored = TRUE
	layer = ABOVE_MOB_LAYER
	plane = GAME_PLANE_UPPER
	blade_dulling = DULLING_BASH
	max_integrity = 300
	dir = SOUTH

/obj/structure/fluff/statue/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("Right-click to access your personal stash. This not only contains the loadout you might've asseembled in the character creation menu, but virtue- and role-specific items as well.")

/obj/structure/fluff/statue/Initialize(mapload)
	. = ..()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/statue/OnCrafted(dirin, user)
	dirin = turn(dirin, 180)
	. = ..()

/obj/structure/fluff/statue/attack_right(mob/user)
	handle_special_items_retrieval(user, src)

/obj/structure/fluff/statue/CanPass(atom/movable/mover, turf/target)
	if(get_dir(loc, mover) == dir)
		if(istype(mover, /mob/living/carbon/human/species/human/northern/goon))
			playsound(src, climb_sound, 100)
			return TRUE
		return 0
	return !density

/obj/structure/fluff/statue/CanAStarPass(ID, to_dir, caller)
	if(to_dir == dir)
		return FALSE // don't even bother climbing over it
	return ..()

/obj/structure/fluff/statue/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER
	if(get_dir(leaving.loc, new_location) == dir)
		if(istype(leaving, /mob/living/carbon/human/species/human/northern/goon))
			playsound(src, climb_sound, 100)
			return	
		leaving.Bump(src)
		return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/statue/gargoyle
	name = "gargoyle"
	desc = "Designed to make the common-folk feel watched, even when they are not."
	icon_state = "gargoyle"

/obj/structure/fluff/statue/aasimar
	name = "aasimar statue"
	desc = "Stone wrought to resemble an Aasimar, the living artifice of the Gods. No life inhabits its eyes, nor \
	strength in its limbs; mortal hands may only imitate divine crafts."
	icon_state = "aasimar"

/obj/structure/fluff/statue/gargoyle/candles
	icon_state = "gargoyle_candles"

/obj/structure/fluff/statue/gargoyle/moss
	icon_state = "mgargoyle"

/obj/structure/fluff/statue/gargoyle/moss/candles
	icon_state = "mgargoyle_candles"

/obj/structure/fluff/statue/knight
	name = "knightly statue"
	desc = "No eyes are visible behind its visor."
	icon_state = "knightstatue_l"

/obj/structure/fluff/statue/astrata
	name = "astrata statue"
	desc = "A stone statue of the sun Goddess Astrata. Bless."
	icon_state = "astrata"
	icon = 'icons/roguetown/misc/tallandwide.dmi'

/obj/structure/fluff/statue/astrata/gold
	name = "ornamental astrata statue"
	desc = "An ornamental stone statue of the sun Goddess Astrata, decorated with golden jewelry. Bless."
	icon_state = "astrata_bling"

//Why are all of these in one giant file.
/obj/structure/fluff/statue/abyssor
	name = "abyssor statue"
	desc = "A slate statue of the ancient god Abyssor. One of many depictions drawn from a dream no doubt. This particular one is horrifying to look at."
	icon_state = "abyssor"
	icon = 'icons/roguetown/misc/tallandwide.dmi'
	pixel_x = -16

/obj/structure/fluff/statue/abyssor/dolomite
	name = "abyssor statue"
	desc = "A rare dolomite statue of the ancient god Abyssor, the Dreamer, He Who Slumbers, \
	patron of the seas and all those that travel by them. He is asleep, and his followers pray \
	fervently that he remains so for a very long time yet."
	icon_state = "abyssor_dolomite"

/obj/structure/fluff/statue/knight/r
	icon_state = "knightstatue_r"

/obj/structure/fluff/statue/knight/interior
	icon_state = "oknightstatue_l"

/obj/structure/fluff/statue/knight/interior/r
	icon_state = "oknightstatue_r"

/obj/structure/fluff/statue/knight/interior/r/bronze
	color = "#ff9c1a"

/obj/structure/fluff/statue/knightalt
	name = "knightly statue"
	desc = "Ever-watchful, faceless, and without independent will. An ideal of chivalry."
	icon_state = "knightstatue2_l"

/obj/structure/fluff/statue/knightalt/r
	icon_state = "knightstatue2_r"


/obj/structure/fluff/statue/myth
	icon_state = "myth"
	desc = "A statue with wildly exaggerated proportions."
	density = TRUE

/obj/structure/fluff/statue/psy
	icon_state = "psy"
	desc = "A statue styled in the manner of an ancient Legionnaire of times long past. One assumes, anyway - \
	such things are no longer seen in the flesh."
	icon = 'icons/roguetown/misc/96x96.dmi'
	pixel_x = -32

/obj/structure/fluff/statue/psybloody
	icon_state = "psy_bloody"
	icon = 'icons/roguetown/misc/96x96.dmi'
	pixel_x = -32


/obj/structure/fluff/statue/small
	desc = "A small statue depicting an elven woman bearing a harp."
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "elfs"

/obj/structure/fluff/statue/pillar
	icon = 'icons/roguetown/misc/structure.dmi'
	icon_state = "pillar"

/obj/structure/fluff/statue/femalestatue
	icon = 'icons/roguetown/misc/ay.dmi'
	desc = "Beauty fades in all but stone."
	icon_state = "1"
	pixel_x = -32
	pixel_y = -16

/obj/structure/fluff/statue/femalestatue1
	icon = 'icons/roguetown/misc/ay.dmi'
	desc = "Beauty fades in all but stone."
	icon_state = "2"
	pixel_x = -32
	pixel_y = -16

/obj/structure/fluff/statue/femalestatue2
	icon = 'icons/roguetown/misc/ay.dmi'
	desc = "Beauty fades in all but stone."
	icon_state = "5"
	pixel_x = -32
	pixel_y = -16

/obj/structure/fluff/statue/femalestatue/zizo
	icon = 'icons/roguetown/misc/ay.dmi'
	desc = "An ancient statue depicting an elven woman."
	icon_state = "4"
	pixel_x = -32
	pixel_y = -16

/obj/structure/fluff/statue/scare
	desc = "An imitation of life to avert famine."
	name = "scarecrow"
	icon_state = "td"

/obj/structure/fluff/statue/tdummy
	name = "practice dummy"
	desc = "Rough fabric wrapped around an interior of plant fibre. Used for practice, or for when one just has some \
	feelings to vent out."
	icon_state = "p_dummy"
	icon = 'icons/roguetown/misc/structure.dmi'

/obj/structure/fluff/statue/tdummy/attack_hand(mob/user)
	if(user.cmode || !user.mind || !isliving(user))
		return ..()
	practice(user, /datum/skill/combat/unarmed, ATTACK_EFFECT_PUNCH)

/obj/structure/fluff/statue/tdummy/attackby(obj/item/attacking_weapon, mob/user, params)
	if(user.cmode || !attacking_weapon.associated_skill || !user.mind || !isliving(user))
		return ..()
	if(attacking_weapon.max_blade_int)
		attacking_weapon.remove_bintegrity(5)
	if(!ispath(attacking_weapon.associated_skill, /datum/skill/combat))
		to_chat(user, span_warning("I don't think this weapon's skill cannot be practiced on a dummy..."))
		return
	practice(user, attacking_weapon.associated_skill, user.used_intent.animname)

/obj/structure/fluff/statue/tdummy/proc/practice(mob/living/living_mob, associated_skill, attack_animation)
	living_mob.changeNext_move(CLICK_CD_MELEE)
	living_mob.stamina_add(rand(4, 6))

	var/probby = (living_mob.STALUC / 10) * 100
	probby = min(probby, 99)
	if(!(living_mob.mobility_flags & MOBILITY_STAND))
		probby = 0
	if(living_mob.STAINT < 3)
		probby = 0
	if(prob(probby) && !living_mob.buckled)
		living_mob.do_attack_animation(src, attack_animation)
		living_mob.visible_message(span_info("[living_mob] trains on [src]!"))
		var/amt2raise = living_mob.STAINT * 0.35
		if(!can_train_combat_skill(living_mob, associated_skill, SKILL_LEVEL_APPRENTICE))
			to_chat(living_mob, span_warning("I've learned all I can from doing this, it's time for the real thing."))
			amt2raise = 0
		if(amt2raise > 0)
			living_mob.mind.add_sleep_experience(associated_skill, amt2raise, FALSE)
		playsound(loc, pick('sound/combat/hits/onwood/education1.ogg', 'sound/combat/hits/onwood/education2.ogg', 'sound/combat/hits/onwood/education3.ogg'), rand(50,100), FALSE)
	else
		living_mob.visible_message(span_danger("[living_mob] trains on [src], but [src] ripostes!"))
		living_mob.AdjustKnockdown(1)
		living_mob.throw_at(get_step(living_mob, get_dir(src,living_mob)), 2, 2, living_mob, spin = FALSE)
		playsound(loc, 'sound/combat/hits/kick/stomp.ogg', 100, TRUE, -1)
	flick(pick("p_dummy_smashed", "p_dummy_smashedalt"), src)

/obj/structure/fluff/statue/spider
	name = "mother"
	icon_state = "spidercore"

/obj/structure/fluff/statue/spider/attackby(obj/item/W, mob/user, params)
	if(istype(W, /obj/item/reagent_containers/food/snacks/rogue/honey/spider))
		if(user.mind)
			if(user.mind.special_role == "Dark Elf")
				playsound(loc,'sound/misc/eat.ogg', rand(30,60), TRUE)
				SSmapping.retainer.delf_contribute += 1
				if(SSmapping.retainer.delf_contribute >= SSmapping.retainer.delf_goal)
					say("YOU HAVE DONE WELL, MY CHILD.",language = /datum/language/elvish)
				else
					say("BRING ME [SSmapping.retainer.delf_goal - SSmapping.retainer.delf_contribute] MORE. I HUNGER.",language = /datum/language/elvish)
				qdel(W)
				return TRUE
	..()

/obj/structure/fluff/statue/evil
	name = "idol"
	desc = "A statue built to the robber-god, Matthios, who stole the gift of fire from the underworld. It is said that he grants the wishes of those pagan bandits (free folk) who feed him money and valuable metals."
	icon_state = "evilidol"
	icon = 'icons/roguetown/misc/structure.dmi'
// What items the idol will accept
	var/treasuretypes = list(
		/obj/item/roguecoin,
		/obj/item/roguegem,
		/obj/item/clothing/ring,
		/obj/item/ingot/gold,
		/obj/item/ingot/silver,
		/obj/item/ingot/blacksteel,
		/obj/item/clothing/neck/roguetown/psicross,
		/obj/item/reagent_containers/glass/cup,
		/obj/item/candle/gold,
		/obj/item/candle/silver,
		/obj/item/candle/candlestick/silver,
		/obj/item/candle/candlestick/gold,
		/obj/item/kitchen/fork/silver,
		/obj/item/kitchen/fork/gold,
		/obj/item/kitchen/spoon/silver,
		/obj/item/kitchen/spoon/gold,
		/obj/item/roguestatue,
		/obj/item/riddleofsteel,
		/obj/item/listenstone,
		/obj/item/clothing/neck/roguetown/shalal,
		/obj/item/clothing/neck/roguetown/horus,
		/obj/item/rogue/painting,
		/obj/item/clothing/head/roguetown/crown/serpcrown,
		/obj/item/clothing/head/roguetown/vampire,
		/obj/item/scomstone,
		/obj/item/rogueweapon/greatsword/psygsword,
		/obj/item/clothing/head/roguetown/circlet,
		/obj/item/carvedgem,	//Some of these aren't particularly worth much, but it'd be REALLY unintuitive for "valuables" to not actually be offerings
		/obj/item/rogueweapon/huntingknife/combat/jadekukri,
		/obj/item/rogueweapon/huntingknife/combat/opalknife,
		/obj/item/rogueweapon/spear/turq,
		/obj/item/rogueweapon/stoneaxe/battle/coral,
		/obj/item/rogueweapon/sword/amber,
		/obj/item/rogueweapon/sword/short/messer/onyxa,
		/obj/item/rogueweapon/huntingknife/idagger/steel/rondel/rose,
		/obj/item/rogueweapon/mace/cudgel/shellrungu,
		/obj/item/clothing/mask/rogue/facemask/carved,
		/obj/item/clothing/neck/roguetown/carved,
		/obj/item/kitchen/fork/carved,
		/obj/item/kitchen/spoon/carved,
		/obj/item/clothing/wrists/roguetown/gem,
		/obj/item/reagent_containers/glass/bowl/carved,
		/obj/item/reagent_containers/glass/bucket/pot/carved,
		/obj/item/clothing/mask/rogue/facemask/carved,
		/obj/item/cooking/platter/carved,
		/obj/item/reagent_containers/lux
	)

/obj/structure/fluff/statue/evil/attackby(obj/item/W, mob/user, params)
	if(!HAS_TRAIT(user, TRAIT_FREEMAN))
		return
	var/donatedamnt = W.get_real_price()
	if(user.mind)
		if(user)
			if(W.flags_1 & HOARDMASTER_SPAWNED_1)
				to_chat(user, span_warning("This item is from the Hoard!"))
				return
			var/proceed_with_offer = FALSE
			for(var/TT in treasuretypes)
				if(istype(W, TT))
					proceed_with_offer = TRUE
					break
			if(proceed_with_offer)
				if(W.get_real_price() <= 0)
					to_chat(user, span_warning("This item is worthless."))
					return
				playsound(loc,'sound/items/carvty.ogg', 50, TRUE)
				log_admin("[user] ([user?.ckey]) submitted [W] ([W.type]) to the Idol, worth [W.get_real_price()]")
				qdel(W)
				for(var/mob/player in GLOB.player_list)
					if(player.mind)
						if(player.mind.has_antag_datum(/datum/antagonist/bandit))
							var/datum/antagonist/bandit/bandit_players = player.mind.has_antag_datum(/datum/antagonist/bandit)
							record_round_statistic(STATS_SHRINE_VALUE, W.get_real_price())
							bandit_players.favor += donatedamnt
							bandit_players.totaldonated += donatedamnt
							to_chat(player, ("<font color='yellow'>[user.name] donates [donatedamnt] to the shrine! You now have [bandit_players.favor] favor.</font>"))
				return //Do not call base - if item sold/given off then stop attacks/hits/other events from using that item on the statue.

			else
				to_chat(user, span_warning("This item isn't a good offering."))
	..()

/obj/structure/fluff/psycross
	name = "stone pantheon cross"
	desc = "Symbol of the Divine Pantheon, the religion of ten - formerly eleven - deities which reigns throughout most of the known world. Their divine order must be maintained."
	icon_state = "cross_undivided_r"
	icon = 'icons/roguetown/misc/tallstructure.dmi'
	break_sound = 'sound/combat/hits/onwood/destroyfurniture.ogg'
	attacked_sound = list('sound/combat/hits/onwood/woodimpact (1).ogg','sound/combat/hits/onwood/woodimpact (2).ogg')
	density = FALSE
	anchored = TRUE
	blade_dulling = DULLING_BASHCHOP
	layer = BELOW_MOB_LAYER
	max_integrity = 100
	var/chance2hear = 30
	buckleverb = "crucifie"
	can_buckle = 1
	buckle_lying = 0
	breakoutextra = 10 MINUTES
	dir = NORTH
	buckle_requires_restraints = 1
	buckle_prevents_pull = 1
	var/divine = TRUE
	obj_flags = UNIQUE_RENAME | CAN_BE_HIT

/obj/structure/fluff/psycross/get_mechanics_examine(mob/user)
	. = ..()
	var/mob/living/living_user = user
	if(user.mind.assigned_role == "Bishop")
		. += span_info("As the Bishop, you can marry two people by having them both bite an apple, then offering it to the cross.")
	else if(istype(living_user) && HAS_TRAIT(living_user, TRAIT_MARRIAGE_CAPABLE))
		. += span_info("As an Eoran, you can marry two people by having them both bite an apple, then offering it to the cross.")

/obj/structure/fluff/psycross/Initialize(mapload)
	. = ..()
	become_hearing_sensitive()
	var/static/list/loc_connections = list(COMSIG_ATOM_EXIT = PROC_REF(on_exit))
	AddElement(/datum/element/connect_loc, loc_connections)

/obj/structure/fluff/psycross/Destroy()
	lose_hearing_sensitivity()
	return ..()

/obj/structure/fluff/psycross/post_buckle_mob(mob/living/M)
	..()
	M.set_mob_offsets("bed_buckle", _x = 0, _y = 2)
	M.setDir(SOUTH)

/obj/structure/fluff/psycross/post_unbuckle_mob(mob/living/M)
	..()
	M.reset_offsets("bed_buckle")

/obj/structure/fluff/psycross/CanPass(atom/movable/mover, turf/target)
	if(istype(mover, /mob/camera))
		return TRUE
	if(get_dir(loc, mover) == dir)
		if(istype(mover, /mob/living/carbon/human/species/human/northern/goon))
			playsound(src, climb_sound, 100)
			return TRUE	
		return FALSE
	return !density

/obj/structure/fluff/psycross/CanAStarPass(ID, to_dir, caller)
	if(to_dir == dir)
		return FALSE // don't even bother climbing over it
	return ..()

/obj/structure/fluff/psycross/proc/on_exit(datum/source, atom/movable/leaving, atom/new_location)
	SIGNAL_HANDLER
	if(istype(leaving, /mob/living/carbon/human/species/human/northern/goon))
		playsound(src, climb_sound, 100)
		return
	if(get_dir(leaving.loc, new_location) == dir)
		leaving.Bump(src)
		return COMPONENT_ATOM_BLOCK_EXIT

/obj/structure/fluff/psycross/copper
	name = "pantheon cross"
	icon_state = "cross_undivided_church"
	break_sound = null
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")
	chance2hear = 66

/obj/structure/fluff/psycross/crafted
	name = "wooden pantheon cross"
	icon_state = "cross_undivided"
	max_integrity = 80
	chance2hear = 10

/obj/structure/fluff/psycross/psycrucifix
	name = "wooden psydonic crucifix"
	desc = "A rarely seen symbol of absolute and devoted certainty, more common in Otava: HE yet lyves. HE yet breathes."
	icon_state = "cross_psy"
	max_integrity = 80
	chance2hear = 10

/obj/structure/fluff/psycross/psycrucifix/stone
	name = "stone psydonic crucifix"
	desc = "Formed of stone, this great Psycross symbolises that HE is forever ENDURING. Considered a rare sight upon the Peaks."
	icon_state = "cross_psy_r"
	max_integrity = 120
	chance2hear = 10

/obj/structure/fluff/psycross/psycrucifix/silver
	name = "silver psydonic crucifix"
	icon_state = "cross_psy_s"
	desc = "Constructed of Blessed Silver, this crucifix symbolises absolute faith in the ONE - For PSYDON WEEPS, for all mortal ilk. PSYDON WEEPS, for all who walk upon the soil. PSYDON WEEPS..."
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")
	max_integrity = 450
	chance2hear = 10

/obj/structure/fluff/psycross/astrata
	name = "wooden astratan cross"
	icon_state = "cross_astrata"
	desc = "A simple cross of carved wood, raised in quiet devotion to Astrata."
	max_integrity = 100
	chance2hear = 20

/obj/structure/fluff/psycross/astrata/stone
	name = "stone astratan cross"
	icon_state = "cross_astrata_r"
	desc = "A towering monument to Astrata. Those who stand beneath it feel the warmth of her light."
	max_integrity = 140
	chance2hear = 20

/obj/structure/fluff/psycross/astrata/golden
	name = "golden astratan cross"
	icon_state = "cross_astrata_u"
	desc = "A radiant monument of gold, devoted to Astrata in her full glory. Its surface gleams with an almost blinding brilliance, catching even the faintest light and casting it forth as a warm, unwavering glow."
	attacked_sound = list("sound/combat/hits/onmetal/metalimpact (1).ogg", "sound/combat/hits/onmetal/metalimpact (2).ogg")
	max_integrity = 400
	chance2hear = 20

/obj/structure/fluff/psycross/zizocross
	name = "inverted cross"
	desc = "An unholy symbol. Blasphemy for most, reverence for few."
	icon_state = "cross_zizo"
	divine = FALSE

/obj/structure/fluff/psycross/zizocross/stone
	name = "stone inverted cross"
	desc = "An unholy symbol. The knowledge that something so sturdy was able to be put up in reverence of the archlych, completely unattended, is a difficult anchovy to swallow for many."
	icon_state = "cross_zizo_r"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/zizocross/golden
	name = "golden inverted cross"
	desc = "An unholy symbol meticulously plated with leaf gold. It stands in defiance to order. The dead will rise."
	icon_state = "cross_zizo_u"
	divine = FALSE
	max_integrity = 350

/obj/structure/fluff/psycross/graggar
	name = "vicious cross"
	desc = "An unholy symbol wrought from stone. It promises glory to the conqueror and chains to the conquered."
	icon_state = "cross_graggar"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/graggar/decorated
	name = "revered vicious cross"
	desc = "An unholy symbol wrought from stone. Meat impaled on spikes and flesh dangling like ribbons off hooks, an offering, proof of conquest, but does he listen?"
	icon_state = "cross_graggar_u"
	divine = FALSE
	max_integrity = 350

/obj/structure/fluff/psycross/matthios
	name = "grinning cross"
	desc = "An unholy stone cross bearing the likeness of drawn daggers and a grinning visage."
	icon_state = "cross_matthios"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/matthios/decorated
	name = "ornate cross"
	desc = "Golden scales dangle from rags and balance the scales. A monument to wealth."
	icon_state = "cross_matthios_u"
	divine = FALSE
	max_integrity = 350

/obj/structure/fluff/psycross/baotha
	name = "spider cross"
	desc = "A gnarled stone cross from which carved spider legs unfurl. You feel like you're being beckoned faintly, like a whisper in your ear."
	icon_state = "cross_baotha"
	divine = FALSE
	max_integrity = 200

/obj/structure/fluff/psycross/baotha/decorated
	name = "webbed spider cross"
	desc = "The spider spreads it's legs, the web unfurls. Just looking at it makes bad memories surface."
	icon_state = "cross_baotha_u"
	divine = FALSE
	max_integrity = 350

#define MARRIAGE_PROMPTS_TIMEOUT 30 // in seconds

/obj/structure/fluff/psycross/attackby(obj/item/W, mob/user, params)
	if(user.mind)
		var/mob/living/living_user = user
		// if there's no bishop inround, you can still get married... as long as there's an eoran. heretics can do it too!
		if(HAS_TRAIT(living_user, TRAIT_MARRIAGE_CAPABLE))
			if(istype(W, /obj/item/reagent_containers/food/snacks/grown/apple))
				var/obj/item/reagent_containers/food/snacks/grown/apple/A = W
				if(A.busy)
					return ..()
				//The MARRIAGE TEST BEGINS
				if(A.bitten_names.len == 2)
					A.rotprocess = null // stops it from rotting mid-ceremony
					A.busy = TRUE // stops spamclicking mid-rite from doing anything
					// Find the groom and bride from those who bit the apple
					var/mob/living/carbon/human/thegroom
					var/mob/living/carbon/human/thebride
					for(var/mob/M in viewers(src, 7))
						// You cannot marry an animal, a corpse, a brainless mob, or someone who is already married.
						if(!ishuman(M))
							continue
						var/mob/living/carbon/human/C = M

						if(C.stat == DEAD || !C.client || C.marriedto)
							continue

						if(C.real_name == A.bitten_names[1])
							thegroom = C
						if(C.real_name == A.bitten_names[2])
							thebride = C

					if(!thegroom || !thebride)
						to_chat(user, span_warn("Both of the betrothed must be within sight of the cross."))
						A.busy = FALSE
						return

					var/list/consent = list("groom" = FALSE, "bride" = FALSE)
					var/list/participants = list(living_user, thebride, thegroom) // used for to_chats

					to_chat(participants, span_green("The rite of marriage begins!")) // mostly so the eoran (who doesn't get any prompts) knows it's working

					// this is going to look like black magic but basically i'm reinventing multithreading here
					// to run two inputs at once, one for each of the betrothed. spawn() makes a copy of the proc
					// including all variables, so normally you can't alter the state between spawn and main. HOWEVER
					// lists are objects, so alterations maid to the list in a spawn()ed proc will affect the state of the main function
					// we will use this trick several more times in this function
					spawn(0)
						consent["groom"] = input(thegroom, "Do you want to marry [thebride]?") as anything in list("Yes", "No")
					spawn(0)
						consent["bride"] = input(thebride, "Do you want to marry [thegroom]?") as anything in list("Yes", "No")

					for(var/i in 1 to MARRIAGE_PROMPTS_TIMEOUT)
						if(consent["groom"] && consent["bride"])
							break
						stoplag(1 SECONDS)
						if(i == MARRIAGE_PROMPTS_TIMEOUT)
							to_chat(participants, span_warning("Marriage prompt timeout!"))
							A.busy = FALSE
							return ..()

					if((consent["groom"] != "Yes") || (consent["bride"] != "Yes"))
						to_chat(participants, span_warning("One of the betrothed has declined the marriage!"))
						A.busy = FALSE
						return ..()

					// setting last names in code is always going to be a buggy mess idk why anyone even tried? we can just prompt them
					var/list/names = list("groom" = FALSE, "bride" = FALSE)
					spawn(0)
						var/gname = input(thegroom, "What would you like your new name to be (leave blank to leave your name unchanged)?")
						names["groom"] = (gname || thegroom.real_name)
					spawn(0)
						var/bname = input(thebride, "What would you like your new name to be (leave blank to leave your name unchanged)?")
						names["bride"] = (bname || thebride.real_name)

					for(var/i in 1 to MARRIAGE_PROMPTS_TIMEOUT)
						if(names["groom"] && names["bride"])
							break
						stoplag(1 SECONDS)
						if(i == MARRIAGE_PROMPTS_TIMEOUT)
							to_chat(participants, span_warning("Marriage prompt timeout!"))
							A.busy = FALSE
							return ..()

					consent = list("groom" = FALSE, "bride" = FALSE)
					to_chat(participants, span_green("[thegroom.real_name] will become [names["groom"]].\n[thebride.real_name] will become [names["bride"]].\n\nIs this acceptable?"))

					// need to give a confirm in case one of them misinputs or the names look ugly next to each other or something
					spawn(0)
						consent["groom"] = input(thegroom, "Are these names acceptable?") as anything in list("Yes", "No")
					spawn(0)
						consent["bride"] = input(thebride, "Are these names acceptable?") as anything in list("Yes", "No")

					for(var/i in 1 to MARRIAGE_PROMPTS_TIMEOUT)
						if(consent["groom"] && consent["bride"])
							break
						stoplag(1 SECONDS)
						if(i == MARRIAGE_PROMPTS_TIMEOUT)
							to_chat(participants, span_warning("Marriage prompt timeout!"))
							A.busy = FALSE
							return ..()

					if((consent["groom"] != "Yes") || (consent["bride"] != "Yes"))
						to_chat(participants, span_warning("One of the betrothed has declined the marriage!"))
						A.busy = FALSE
						return ..()

					thegroom.change_name(names["groom"])
					thebride.change_name(names["bride"])

					thegroom.marriedto = thebride.real_name
					thebride.marriedto = thegroom.real_name

					thegroom.adjust_triumphs(1)
					thebride.adjust_triumphs(1)

					priority_announce("[thegroom.real_name] has married [thebride.real_name]!", title = "Holy Union!", sound = 'sound/misc/bell.ogg')
					record_round_statistic(STATS_MARRIAGES_MADE)
					A.busy = FALSE
					return ..()
	return ..()

#undef MARRIAGE_PROMPTS_TIMEOUT

/obj/structure/fluff/psycross/copper/Destroy()
	addomen("psycross")
	..()

/obj/structure/fluff/psycross/proc/AOE_flash(mob/user, range = 15, power = 5, targeted = FALSE)
	var/list/mob/targets = get_flash_targets(get_turf(src), range, FALSE)
	for(var/mob/living/carbon/C in targets)
		flash_carbon(C, user, power, targeted, TRUE)
	return TRUE

/obj/structure/fluff/psycross/proc/get_flash_targets(atom/target_loc, range = 15)
	if(!target_loc)
		target_loc = loc
	if(isturf(target_loc) || (ismob(target_loc) && isturf(target_loc.loc)))
		return viewers(range, get_turf(target_loc))
	else
		return typecache_filter_list(target_loc.GetAllContents(), GLOB.typecache_living)

/obj/structure/fluff/psycross/proc/flash_carbon(mob/living/carbon/M, mob/user, power = 15, targeted = TRUE, generic_message = FALSE)
	if(!istype(M))
		return
	if(user)
		log_combat(user, M, "[targeted? "flashed(targeted)" : "flashed(AOE)"]", src)
	else //caused by emp/remote signal
		M.log_message("was [targeted? "flashed(targeted)" : "flashed(AOE)"]",LOG_ATTACK)
	if(generic_message && M != user)
		to_chat(M, span_danger("[src] emits a blinding light!"))
	if(M.flash_act())
		var/diff = power - M.confused
		M.confused += min(power, diff)

/obj/structure/fluff/psycross/proc/summon_martyr_weapon_tgui(mob/user)
	if(!user.mind)
		return

	var/list/weapon_choices = list(
		"Sword" = CALLBACK(src, PROC_REF(summon_and_equip), user, /obj/item/rogueweapon/sword/long/martyr),
		"Axe" = CALLBACK(src, PROC_REF(summon_and_equip), user, /obj/item/rogueweapon/greataxe/steel/doublehead/martyr),
		"Mace" = CALLBACK(src, PROC_REF(summon_and_equip), user, /obj/item/rogueweapon/mace/goden/martyr),
		"Trident" = CALLBACK(src, PROC_REF(summon_and_equip), user, /obj/item/rogueweapon/spear/partizan/martyr)
	)

	var/result = tgui_input_list(user, "Choose a martyr weapon to summon:", "Martyr Weapon", weapon_choices)

	if(result && weapon_choices[result])
		var/datum/callback/selected_callback = weapon_choices[result]
		selected_callback.Invoke()
	else
		to_chat(user, span_warning("No weapon was chosen."))

/obj/structure/fluff/psycross/proc/summon_and_equip(mob/user, obj/item/rogueweapon/weapontype)
	var/obj/item/rogueweapon/old_weapon = SSroguemachine.martyrweapon
	var/integrity

	if(old_weapon)
		integrity = old_weapon.obj_integrity
		old_weapon.visible_message(span_danger("[old_weapon] dissolves into mere dust, and flitters away - unbound."))
		SSroguemachine.martyrweapon = null
		qdel(old_weapon)

	var/obj/item/rogueweapon/new_weapon = new weapontype(src.loc)
	new_weapon.obj_integrity = integrity
	SSroguemachine.martyrweapon = new_weapon

	if(user.put_in_hands(new_weapon))
		to_chat(user, span_notice("[new_weapon] appears in your hand."))
	else
		to_chat(user, span_warning("Your hands are full! [new_weapon] falls to your feet."))

	return new_weapon

/obj/structure/fluff/psycross/attack_hand(mob/user)
	. = ..()
	if(.)
		return
	if(user.job != "Martyr")
		return
	if((HAS_TRAIT(user, TRAIT_NOPAIN) && HAS_TRAIT(user, TRAIT_STRENGTH_UNCAPPED) && HAS_TRAIT(user, TRAIT_BLOODLOSS_IMMUNE))) // So that the martyr could not change weapons during his special ability... I do not know how to make it smarter.
		return
	summon_martyr_weapon_tgui(user)

/obj/structure/fluff/beach_umbrella/security
	icon_state = "hos_brella"

/obj/structure/fluff/beach_umbrella/science
	icon_state = "rd_brella"

/obj/structure/fluff/beach_umbrella/engine
	icon_state = "ce_brella"

/obj/structure/fluff/beach_umbrella/cap
	icon_state = "cap_brella"

/obj/structure/fluff/beach_umbrella/syndi
	icon_state = "syndi_brella"

/obj/structure/fluff/clockwork
	name = "Clockwork Fluff"
	icon = 'icons/obj/clockwork_objects.dmi'
	deconstructible = FALSE

/obj/structure/fluff/clockwork/alloy_shards
	name = "replicant alloy shards"
	desc = ""
	icon_state = "alloy_shards"

/obj/structure/fluff/clockwork/alloy_shards/small
	icon_state = "shard_small1"

/obj/structure/fluff/clockwork/alloy_shards/medium
	icon_state = "shard_medium1"

/obj/structure/fluff/clockwork/alloy_shards/medium_gearbit
	icon_state = "gear_bit1"

/obj/structure/fluff/clockwork/alloy_shards/large
	icon_state = "shard_large1"

/obj/structure/fluff/clockwork/blind_eye
	name = "blind eye"
	desc = ""
	icon_state = "blind_eye"

/obj/structure/fluff/clockwork/fallen_armor
	name = "fallen armor"
	desc = ""
	icon_state = "fallen_armor"

/obj/structure/fluff/clockwork/clockgolem_remains
	name = "clockwork golem scrap"
	desc = ""
	icon_state = "clockgolem_dead"

/obj/structure/fluff/headstake
	name = "head on a stake"
	desc = ""
	icon = 'icons/roguetown/items/natural.dmi'
	icon_state = "headstake"
	density = FALSE
	anchored = TRUE
	dir = SOUTH
	var/obj/item/grown/log/tree/stake/stake
	var/obj/item/bodypart/head/victim

/obj/structure/fluff/headstake/CheckParts(list/parts_list)
	..()
	victim = locate(/obj/item/bodypart/head) in parts_list
	name = "[victim.name] on a stake"
	update_icon()
	stake = locate(/obj/item/grown/log/tree/stake) in parts_list

///obj/structure/fluff/headstake/Initialize(mapload)
//	. = ..()

/obj/structure/fluff/headstake/OnCrafted(dirin, user)
	dir = SOUTH
	pixel_x = rand(-8, 8)
	return

/obj/structure/fluff/headstake/update_icon()
	..()
	var/obj/item/bodypart/head/H = locate() in contents
	var/mutable_appearance/MA = new()
	if(H)
		MA.copy_overlays(H)
		H.pixel_y = rand(9, 11)
		H.pixel_x = pixel_x
		H.dir = SOUTH
		add_overlay(H)

/obj/structure/fluff/headstake/attack_hand(mob/user)
	. = ..()
	if(.)
		return
	to_chat(user, span_notice("I take down [src]."))
	if (victim)
		victim.forceMove(drop_location())
		victim = null
	stake.forceMove(drop_location())
	stake = null
	qdel(src)

/obj/structure/fluff/headstake/deconstruct()
	if (victim)
		victim.forceMove(drop_location())
		victim = null
	stake.forceMove(drop_location())
	stake = null
	qdel(src)

/obj/structure/bars/passage/shutter/bookcase
	name = "Empty Bookcase"
	desc = "Refuge for few, an irrelevance to most."
	icon_state = "decoybookcase0"

/obj/structure/bars/passage/shutter/bookcase/redstone_triggered()
	if(obj_broken)
		return
	if(density)
		icon_state = "decoybookcase1"
		density = FALSE
		set_opacity(FALSE)
	else
		icon_state = "decoybookcase0"
		density = TRUE
		set_opacity(TRUE)

// This is from the Druid Grove remap ages back. Turning it into a proper subtype for faster init. or whatever reason ur supposed
// to do it.
/obj/effect/wisp/prestidigitation/willowwisp
	name = "Will-o'-the-wisp"
	desc = "A small, fiery ball of light made up of mystical energy."

/obj/effect/wisp/prestidigitation/runelight
	name = "arcyne mote"
	desc = "An ethereal ball of pure light, manifested through an arcyne sigil."
