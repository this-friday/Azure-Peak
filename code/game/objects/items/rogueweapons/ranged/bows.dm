//intent datums ฅ^•ﻌ•^ฅ

/proc/bow_draw_sound(chargetime)
	switch(chargetime)
		if(0 to 10)
			return 'sound/combat/Ranged/bow-draw-01-8ds.ogg'
		if(10 to 14)
			return 'sound/combat/Ranged/bow-draw-01-12ds.ogg'
		if(14 to 19)
			return 'sound/combat/Ranged/bow-draw-01.ogg'
		else
			return 'sound/combat/Ranged/bow-draw-01-22ds.ogg'

/datum/intent/shoot/bow
	chargetime = 1 //used for edge cases only, the bow's get_draw_time() handles the actual number
	chargedrain = BOW_CHARGEDRAIN
	charging_slowdown = 3

/datum/intent/shoot/bow/can_charge(atom/clicked_object)
	if(mastermob?.get_num_arms(FALSE) < 2 || mastermob.get_inactive_held_item())
		to_chat(mastermob, span_warning("I need a free hand to draw [masteritem]!"))
		return FALSE
	if(istype(clicked_object, /obj/item/quiver) && istype(mastermob?.get_active_held_item(), /obj/item/gun/ballistic))
		return FALSE
	if(needs_loaded_launcher && !launcher_is_loaded())
		to_chat(mastermob, span_warning("I have nothing nocked!"))
		return FALSE

	return TRUE

/datum/intent/shoot/bow/prewarning()
	if(mastermob)
		mastermob.visible_message(span_warning("[mastermob] draws [masteritem]!"))
		playsound(mastermob, bow_draw_sound(get_chargetime()), 100, FALSE, channel = CHANNEL_WEAPON_DRAW)

/datum/intent/shoot/bow/get_chargetime() //draw speed lives on the bow itself so players and NPCs share one curve. damage is handled below in /obj/item/gun/ballistic/revolver/grenadelauncher/bow/process_fire
	if(mastermob && chargetime)
		var/obj/item/gun/ballistic/revolver/grenadelauncher/bow/bow = masteritem
		if(istype(bow))
			var/newtime = bow.get_draw_time(mastermob, FALSE)
			if(newtime)
				return newtime
	return chargetime //if a bow somehow gets drawn by something that doesn't fulfill the above we can use the intent value

/datum/intent/arc/bow
	chargetime = 1
	chargedrain = BOW_CHARGEDRAIN
	charging_slowdown = 3

/datum/intent/arc/bow/can_charge(atom/clicked_object)
	if(mastermob?.get_num_arms(FALSE) < 2 || mastermob.get_inactive_held_item())
		to_chat(mastermob, span_warning("I need a free hand to draw [masteritem]!"))
		return FALSE
	if(istype(clicked_object, /obj/item/quiver) && istype(mastermob?.get_active_held_item(), /obj/item/gun/ballistic))
		return FALSE
	if(needs_loaded_launcher && !launcher_is_loaded())
		to_chat(mastermob, span_warning("I have nothing nocked!"))
		return FALSE

	return TRUE

/datum/intent/arc/bow/prewarning()
	if(mastermob)
		mastermob.visible_message(span_warning("[mastermob] draws [masteritem] in an arc!"))
		playsound(mastermob, bow_draw_sound(get_chargetime()), 100, FALSE, channel = CHANNEL_WEAPON_DRAW)

/datum/intent/arc/bow/get_chargetime() //same curve as above, but slower and with a higher floor
	if(mastermob && chargetime)
		var/obj/item/gun/ballistic/revolver/grenadelauncher/bow/bow = masteritem
		if(istype(bow))
			var/newtime = bow.get_draw_time(mastermob, TRUE)
			if(newtime)
				return newtime
	return chargetime

//bow objs ฅ^•ﻌ•^ฅ

/obj/item/gun/ballistic/revolver/grenadelauncher/bow
	has_item_quality = TRUE
	name = "oak hunting bow"
	desc = "A typical hunting bow used by peasants, hunters and levies in absence of more powerful warbows, \
	it is too weak to pose real threat to armour but in skilled hands a deadly tool all the same."
	icon = 'icons/roguetown/weapons/ranged32.dmi'
	icon_state = "bow"
	item_state = "bow"
	experimental_onhip = TRUE
	experimental_onback = TRUE
	possible_item_intents = list(
		/datum/intent/shoot/bow,
		/datum/intent/arc/bow,
		INTENT_GENERIC,
		)
	mag_type = /obj/item/ammo_box/magazine/internal/shot/bow
	fire_sound = 'sound/combat/Ranged/flatbow-shot-01.ogg'
	slot_flags = ITEM_SLOT_BACK|ITEM_SLOT_HIP
	w_class = WEIGHT_CLASS_BULKY
	randomspread = 0
	spread = 0
	can_parry = TRUE
	force = 10
	verbage = "nock"
	cartridge_wording = "arrow"
	load_sound = 'sound/foley/nockarrow.ogg'
	obj_flags = UNIQUE_RENAME
	var/heavy_bow = FALSE //flavour flag for bows with a STR-scaled draw. the scaling itself is draw_per_str
	cartridge_articles = "an"
	var/spill_ammo_on_drop = TRUE
	ranged_skill = /datum/skill/combat/bows
	release_drain = BOW_RELEASEDRAIN
	draw_base = BOW_DRAW_BASE
	draw_floor = BOW_DRAW_FLOOR
	uses_draw_curve = TRUE
	per_scales_damage = TRUE
	early_release_acc_penalty = BOW_EARLY_RELEASE_ACC_PENALTY
	early_release_embed_mult = BOW_EARLY_RELEASE_EMBED_MULT

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/can_quick_load(mob/user)
	if(user.get_num_arms(FALSE) < 2 || user.get_inactive_held_item())
		to_chat(user, span_warning("I need a free hand to nock [src]!"))
		return FALSE
	return TRUE

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/get_mechanics_examine(mob/user)
	. = ..()
	. += span_info("My <b>ARCHERY</b> skill defines how precise my shots are and how fast I can draw.")
	. += span_info("Bows increase in damage the higher your <b>PERCEPTION</b>.")
	. += span_info("When I shoot a target too close or too far away, I will only hit the chest.")
	. += span_info("Bows with a heavy draw, such as longbows, have an increased draw time for characters with low <b>STRENGTH</b>.")
	. += span_info("Nocking straight from a quiver requires my other hand to be free.")

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/Initialize(mapload)
	. = ..()
	if(heavy_bow)
		desc += " <b>Has a heavy draw.</b>"

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/getonmobprop(tag)
	. = ..()
	if(tag)
		switch(tag)
			if("gen")
				return list(
					"shrink" = 0.7,
					"sx" = -3,
					"sy" = 0,
					"nx" = 6,
					"ny" = 1,
					"wx" = -1,
					"wy" = 1,
					"ex" = -2,
					"ey" = 1,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 0,
					"nturn" = 9,
					"sturn" = -100,
					"wturn" = -102,
					"eturn" = 10,
					"nflip" = 1,
					"sflip" = 8,
					"wflip" = 8,
					"eflip" = 1,
					)
			if("onbelt")
				return list(
					"shrink" = 0.7,
					"sx" = 0,
					"sy" = -3,
					"nx" = 3,
					"ny" = -5,
					"wx" = -7,
					"wy" = -5,
					"ex" = 2,
					"ey" = -5,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 0,
					"sflip" = 8,
					"wflip" = 0,
					"eflip" = 8,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0)
			if("onback")
				return list(
					"shrink" = 0.7,
					"sx" = -1,
					"sy" = 0,
					"nx" = 0,
					"ny" = 1,
					"wx" = -2,
					"wy" = 0,
					"ex" = 0,
					"ey" = 0,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 0,
					"sflip" = 0,
					"wflip" = 0,
					"eflip" = 8,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0,)

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/shoot_with_empty_chamber()
	return

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/dropped()
	. = ..()
	if(chambered && spill_ammo_on_drop)
		chambered = null
		var/num_unloaded = 0
		for(var/obj/item/ammo_casing/CB in get_ammo_list(FALSE, TRUE))
			CB.forceMove(drop_location())
//			CB.bounce_away(FALSE, NONE)
			num_unloaded++
		if (num_unloaded)
			update_icon()

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/process_fire(atom/target, mob/living/user, message = TRUE, params = null, zone_override = "", bonus_spread = 0)
	if(user.get_inactive_held_item() || user.get_num_arms(FALSE) < 2)
		to_chat(user, span_warning("I need a free hand to fire \the [src]!"))
		return FALSE
	spread = get_ranged_spread(user)
	for(var/obj/item/ammo_casing/CB in get_ammo_list(FALSE, TRUE))
		var/obj/projectile/BB = CB.BB
		apply_ranged_accuracy(BB, user)
		apply_early_release_penalty(BB, user)
		BB.damage *= damfactor * get_per_damage_scaling(user)
	. = ..()
	if(.)
		pay_release_drain(user)

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/update_icon()
	..()

	var/matrix/mat = matrix()
	mat.Translate(0,0)

	cut_overlays()
	if(chambered)
		var/mutable_appearance/ammo = mutable_appearance(chambered.icon, chambered.icon_state)
		ammo.transform = mat
		add_overlay(ammo)

	if(!ismob(loc))
		return
	var/mob/M = loc
	M.update_inv_hands()

/obj/item/ammo_box/magazine/internal/shot/bow
	ammo_type = /obj/item/ammo_casing/caseless/rogue/arrow
	caliber = "arrow"
	max_ammo = 1
	start_empty = TRUE

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/recurve
	name = "recurve bow"
	desc = "A medium length composite bow of glued horn, wood, and sinew with good shooting \
	characteristics."
	icon_state = "recurve_bow"
	release_drain = RECURVE_RELEASEDRAIN
	force = 9

/*
/obj/item/gun/ballistic/revolver/grenadelauncher/bow/recurve/getonmobprop(tag)
	. = ..()
	if(tag)
		switch(tag)
			if("gen")
				return list(
					"shrink" = 0.6,
					"sx" = -3,
					"sy" = 0,
					"nx" = 6,
					"ny" = 1,
					"wx" = -1,
					"wy" = 1,
					"ex" = -2,
					"ey" = 1,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 0,
					"nturn" = 9,
					"sturn" = -100,
					"wturn" = -102,
					"eturn" = 10,
					"nflip" = 1,
					"sflip" = 8,
					"wflip" = 8,
					"eflip" = 1,
					)
			if("onbelt")
				return list(
					"shrink" = 0.6,
					"sx" = 0,
					"sy" = -3,
					"nx" = 3,
					"ny" = -5,
					"wx" = -7,
					"wy" = -5,
					"ex" = 2,
					"ey" = -5,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 0,
					"sflip" = 8,
					"wflip" = 0,
					"eflip" = 8,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0)
			if("onback")
				return list(
					"shrink" = 0.6,
					"sx" = -1,
					"sy" = 0,
					"nx" = 0,
					"ny" = 1,
					"wx" = -2,
					"wy" = 0,
					"ex" = 0,
					"ey" = 0,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 0,
					"sflip" = 0,
					"wflip" = 0,
					"eflip" = 8,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0,)
*/
/obj/item/gun/ballistic/revolver/grenadelauncher/bow/longbow
	name = "yew longbow"
	desc = "A sturdy warbow made of a tillered yew stave. It's difficult to handle, but the \
	power is worth the effort."
	icon_state = "longbow"
	slot_flags = ITEM_SLOT_BACK
	damfactor = 1.3
	accfactor = 0.9
	heavy_bow = TRUE
	release_drain = LONGBOW_RELEASEDRAIN
	draw_base = LONGBOW_DRAW_BASE
	draw_floor = LONGBOW_DRAW_FLOOR
	draw_per_str = LONGBOW_DRAW_PER_STR
/*
/obj/item/gun/ballistic/revolver/grenadelauncher/bow/longbow/getonmobprop(tag)
	. = ..()
	if(tag)
		switch(tag)
			if("gen")
				return list(
					"shrink" = 0.6,
					"sx" = -3,
					"sy" = 0,
					"nx" = 6,
					"ny" = 1,
					"wx" = -1,
					"wy" = 0,
					"ex" = -2,
					"ey" = 0,
					"northabove" = 0,
					"southabove" = 1,
					"eastabove" = 1,
					"westabove" = 0,
					"nturn" = 9,
					"sturn" = -100,
					"wturn" = -102,
					"eturn" = 10,
					"nflip" = 1,
					"sflip" = 8,
					"wflip" = 8,
					"eflip" = 1,
					)
			if("onback")
				return list(
					"shrink" = 0.6,
					"sx" = 0,
					"sy" = 1,
					"nx" = 0,
					"ny" = 0,
					"wx" = -1,
					"wy" = 1,
					"ex" = 0,
					"ey" = 0,
					"nturn" = 0,
					"sturn" = 0,
					"wturn" = 0,
					"eturn" = 0,
					"nflip" = 0,
					"sflip" = 0,
					"wflip" = 0,
					"eflip" = 8,
					"northabove" = 1,
					"southabove" = 0,
					"eastabove" = 0,
					"westabove" = 0,
					)
*/

//Unique Bows

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/watchman
	name = "yew hunting bow"
	desc = "A typical hunting bow made out of sturdier wood for the town guard with Azure wrapping on the stave, \
	it is too weak to pose real threat to armour but in skilled hands a deadly tool all the same."
	icon_state = "bow_watchman"

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/warden
	name = "blackhorn bow"
	desc = "When a northern black-horned saiga is old enough, it will shed its two-metre long antlers. As time passes, they harden progressively more but keep a degree of flexibility that can outdo even yew.\
		Wardens often collect such antlers in the rare occasion they are found and send them to be filed, strung and treated by a master bowyer. Such tradition carries merit even todae, \
		and thus one can see Azurian wardens carrying their endemic blackhorn bows with pride."
	icon_state = "bow_warden"

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/longbow/warden
	name = "blackhorn longbow"
	desc = "When a northern black-horned saiga is old enough, it will shed its two-metre long antlers. As time passes, they harden progressively more but keep a degree of flexibility that can outdo even yew.\
		Wardens often collect such antlers in the rare occasion they are found and send them to be filed, strung and treated by a master bowyer. The end result is a war bow such as this one."
	icon_state = "longbow_warden"

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/recurve/steppesman
	name = "aavnic riding bow"
	desc = "A short recurve warbow made for the express purpose of shooting on saigaback, a skill every archer in Aavnr takes much more seriously than their Northern counterparts. Every seasoned Druzhina is themselves a good bowyer and usually makes their own bow, this one is made with the purpure-ish crimson wood of a Vörötslevé tree."
	icon_state = "recurve_bow_steppesman"

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/recurve/blackoak
	name = "woad recurve bow"
	desc = "A medium length composite bow of glued horn, wood, and sinew with fine shooting characteristics. Hewn from a living Black Oak branch, it carries the quiet strength of untouched groves; unyielding, unbroken, and fiercely guarded from the hands of Man."
	icon_state = "recurve_bow_blackoak"

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/towner
	name = "hunting flatbow"
	desc = "A short flatbow made of Hazel from the Azurian Enclave's forests, historically favoured by wood elves and thus becoming a tradition of the local hunters. Compared to similar hunting bows, this one's marginally more accurate."
	icon_state = "bow_towner"
	accfactor = 1.1

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/longbow/towner
	name = "hunting longbow"
	desc = "A sturdy longbow made of Black Locust from a small dense reserve in Mount Decapitation. It doesn't have a draw as heavy as that of the war longbow, but it preserves its accuracy this way."
	icon_state = "longbow_towner"
	damfactor = 1.15
	accfactor = 1

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short
	name = "short bow"
	desc = "As the eagle was killed by the arrow winged with his own feather, so the hand of the world is wounded by its own skill."
	icon_state = "bow_short"
	item_state = "bow_short"
	possible_item_intents = list(
		/datum/intent/shoot/bow/short,
		/datum/intent/arc/bow/short,
		INTENT_GENERIC,
		)
	randomspread = 1
	spread = 1
	force = 9
	damfactor = 0.9
	release_drain = SHORTBOW_RELEASEDRAIN
	draw_base = SHORTBOW_DRAW_BASE
	draw_floor = SHORTBOW_DRAW_FLOOR

/datum/intent/shoot/bow/short
	chargedrain = SHORTBOW_CHARGEDRAIN
	charging_slowdown = 2.5

/datum/intent/arc/bow/short
	chargedrain = SHORTBOW_CHARGEDRAIN
	charging_slowdown = 2.5

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint
	name = "painted bow"
	desc = "A strange painted bow, seems volatile, like it could dust apart into nothing but liquid."
	icon_state = "paintbow"
	item_state = "paintbow"
	item_flags = DROPDEL
	spill_ammo_on_drop = FALSE
	var/dust_timer_id
	mag_type = /obj/item/ammo_box/magazine/internal/shot/bow/paint

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/Initialize(mapload)
	. = ..()
	start_dust_timer(30 SECONDS)
	if(magazine)
		chamber_round()

	update_icon()
	if(ismob(loc))
		var/mob/M = loc
		M.update_inv_hands()

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/Destroy()
	if(dust_timer_id)
		deltimer(dust_timer_id)
	return ..()

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/proc/start_dust_timer(duration)
	if(dust_timer_id)
		deltimer(dust_timer_id)
	dust_timer_id = addtimer(CALLBACK(src, PROC_REF(check_and_dust)), duration, TIMER_STOPPABLE)

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/proc/check_and_dust()
	dust_timer_id = null

	if(ismob(loc))
		var/mob/living/L = loc
		if(L.get_active_held_item() == src)
			start_dust_timer(5 SECONDS)
			return

	src.visible_message(span_warning("\The [src] dissolves into shimmering paint dust and vanishes!"))
	qdel(src)

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/process_fire(atom/target, mob/living/user, message = TRUE, params = null, zone_override = "", bonus_spread = 0)
	var/obj/item/ammo_casing/C = chambered

	if(istype(C, /obj/item/ammo_casing/caseless/rogue/arrow/iron/paint) && C.BB)
		var/obj/projectile/bullet/reusable/arrow/iron/paint/paint_arrow = C.BB
		if(istype(paint_arrow))
			paint_arrow.primed = TRUE

	. = ..()

	// If we successfully fired, safely commit suicide
	if(.)
		var/turf/T = get_turf(src)
		if(T)
			T.visible_message(span_danger("\The [src] turns to paint dust from the shot's force!"))
		qdel(src)

/obj/item/ammo_box/magazine/internal/shot/bow/paint
	ammo_type = /obj/item/ammo_casing/caseless/rogue/arrow/iron/paint
	start_empty = FALSE // Spawns preloaded with the arrow
	max_ammo = 1

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/attack_hand(mob/user)
	if(loc == user && user.is_holding(src))
		to_chat(user, span_warning("\The [src]'s arrow is tightly bound to the string by magical paint!"))
		return FALSE
	return ..()

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/attack_self(mob/living/user)
	to_chat(user, span_warning("\The [src]'s arrow is permanently fused to the frame!"))
	return FALSE

/obj/item/gun/ballistic/revolver/grenadelauncher/bow/short/paint/attackby(obj/item/A, mob/user, params)
	if(istype(A, /obj/item/ammo_box/magazine) || istype(A, /obj/item/ammo_casing) || istype(A, /obj/item/ammo_box))
		to_chat(user, span_warning("\The [src] cannot be loaded with any other ammunition!"))
		return FALSE
	return ..()
